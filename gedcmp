#!/usr/bin/env perl

# Author Nigel Horne: njh@bandsman.co.uk
# Copyright (C) 2016-2022, Nigel Horne

# Usage is subject to licence terms.
# The licence terms of this software are as follows:
# Personal single user, single computer use: GPL2
# All other users (including Commercial, Charity, Educational, Government)
#	must apply in writing for a licence for use from Nigel Horne at the
#	above e-mail.

# Compare two gedcoms for differences

use strict;
use warnings;
use autodie qw(:all);

no lib '.';

my @modules;

# Install Pre-requisites
BEGIN {
	@modules = (
		'DateTime::Format::Genealogy', 'Gedcom', 'Term::ANSIColor',
		'Lingua::EN::NameParse', 'Lingua::EN::NameCase', 'Tie::Static',
	);

	my @needfull;
	foreach my $module(@modules) {
		eval "use $module";
		if($@) {
			push @needfull, $module;
		}
	}
	if(scalar(@needfull)) {
		my $list = join(' ', @needfull);
		print "Installing $list\n";
		system("cpan -i $list");
	}
}

foreach my $module(@modules) {
	my $version;
	if($module =~ /(.+)\s(.+)$/) {
		$module = $1;
		$version = $2;
	}
	if($module =~ /.+\/(.+)/) {
		$module = $1;
	}
	eval "require $module";
	if($@) {
		die $@;
	}
	$module->import();
	# if($version && ($module::VERSION < $version)) {
		# die "$module: need $version got ", $module::VERSIOn;
	# }
}

die "Usage: $0 filename1 filename2" unless($ARGV[0] && $ARGV[1]);

my $ged1 = Gedcom->new(gedcom_file => $ARGV[0], read_only => 1);
my $ged2 = Gedcom->new(gedcom_file => $ARGV[1], read_only => 1);
my $nameparser = Lingua::EN::NameParse->new(extended_titles => 1, initials => 1);
my $dfg = DateTime::Format::Genealogy->new();

my @everyone1 = $ged1->individuals();

print "\"person\",\"$ARGV[0]\",\"$ARGV[1]\",\"note\"\n";

while(my $person1 = shift(@everyone1)) {
	my @diffs = ();
	my @people2 = $ged2->get_individual($person1->as_string({middle_names => 1}));
	while(my $person2 = shift(@people2)) {
		my @rc = compare($person1, $person2);
		if(scalar(@rc)) {
			@diffs = @rc;	# This one doesn't match
		} else {
			@diffs = ();	# Found match
			last;
		}
	}
	if(scalar(@diffs)) {
		print '"', $person1->as_string(include_years => 1), '","', join('","', map { defined($_) ? $_ : '' } @diffs), "\"\n";
	}
}

sub compare
{
	my ($person1, $person2) = @_;

	my $string1 = $person1->as_string();

	return unless($string1 eq $person2->as_string());

	my $dob1 = get_value({ person => $person1, value => 'birth date' });
	my $dob2 = get_value({ person => $person2, value => 'birth date' });

	if($dob1 && !$dob2) {
		return ($dob1, undef, "date of birth is missing from $ARGV[1]");
	} elsif($dob2 && !$dob1) {
		return (undef, $dob2, "date of birth is missing from $ARGV[0]");
	} elsif($dob1 && $dob2) {
		$dob1 =~ s/^abt\.\s/abt /i;
		$dob2 =~ s/^abt\.\s/abt /i;
		if(($dob1 ne $dob2) && (datecmp($dob1, $dob2) != 0)) {
			return ($dob1, $dob2, 'date of birth differs');
		}
	}

	my $dod1 = get_value({ person => $person1, value => 'death date' });
	my $dod2 = get_value({ person => $person2, value => 'death date' });

	if($dod1 && !$dod2) {
		return ($dod1, undef, "date of death is missing from $ARGV[1]");
	} elsif($dod2 && !$dod1) {
		return (undef, $dod2, "date of death is missing from $ARGV[0]");
	} elsif($dod1 && $dod2) {
		$dod1 =~ s/^abt\.\s/abt /i;
		$dod2 =~ s/^abt\.\s/abt /i;
		if(($dod1 ne $dod2) && (datecmp($dod1, $dod2) != 0)) {
			return ($dod1, $dod2, 'date of death differs');
		}
	}

	my @spouses1 = $person1->spouse();
	my @spouses2 = $person2->spouse();
	if(scalar(@spouses1) != scalar(@spouses2)) {
		return (scalar(@spouses1), scalar(@spouses2), 'number of spouses differs');
	}

	my $mother1 = $person1->mother();
	my $mother2 = $person2->mother();
	if($mother1 && !$mother2) {
		return ($mother1->as_string(), undef, "mother is missing from $ARGV[1]");
	} elsif($mother2 && !$mother1) {
		return (undef, $mother2->as_string(), "mother is missing from $ARGV[0]");
	}

	my $father1 = $person1->father();
	my $father2 = $person2->father();
	if($father1 && !$father2) {
		return ($father1->as_string(), undef, "father is missing from $ARGV[1]");
	} elsif($father2 && !$father1) {
		return (undef, $father2->as_string(), "father is missing from $ARGV[0]");
	}

	return;
}

sub Gedcom::Individual::as_string
{
	my $self = shift;
	my %args = (ref($_[0]) eq 'HASH') ? %{$_[0]} : @_;

	my $name;

	if($args{'use_aka'}) {
		my $n2 = $self->tag_record('NAME', 2);
		if(defined($n2) && (ref($n2) eq 'Gedcom::Record') && defined($n2->type()) && ($n2->type() eq 'AlsoKnownAs') && ($n2->value() ne $self->name())) {
			$name = $n2->value();
		} else {
			return;
		}
	} else {
		$name = $self->name();
	}

	$name =~ s/\///g;
	$nameparser->parse($name);

	my %name_components = $nameparser->components();
	my $first_name = $name_components{'given_name_1'};
	my $middle_name = $name_components{'middle_name'};
	my $surname = $name_components{'surname_1'};
	if($name_components{'surname_2'}) {
		$surname .= ' ' . $name_components{'surname_2'};
	}
	if($name_components{'non_matching'}) {
		if($name =~ /(\w{2,})\s(\w{2,})\s(\w+)\s(\w{2,})/) {
			# Name parser doesn't parse names like this very well
			$first_name = $1;
			$middle_name = "$2 $3";
			$surname = $4;
		} else {
			if($surname) {
				$surname .= ' ';
			}
			my $rest = $name_components{'non_matching'};
			if($rest =~ /(.+) $/) {
				$rest = $1;
			}
			$surname .= $rest;
		}
	} elsif(!defined($surname)) {
		$surname = $self->surname();
	}

	my $has_maiden_name;
	if($args{'nee'}) {
		my $sex = get_value({ person => $self, value => 'sex' });
		if(defined($sex) && ($sex eq 'F')) {
			if(my $husband = $self->husband()) {
				$surname = $husband->surname();
				$has_maiden_name = 1;
			} elsif(my $spouse = $self->spouse()) {
				$surname = $spouse->surname();
				$has_maiden_name = 1;
			}
		}
	}

	my $rc;

	if($args{'title'} && $name_components{'title_1'}) {
		$rc .= $name_components{'title_1'};
		if($rc) {
			$rc .= ' ';
		}
	}

	if($first_name) {
		$rc .= $first_name;
	} elsif($self->given_names()) {
		$rc .= $self->given_names();
	} elsif($args{'print_unknown'}) {
		$rc .= ' ?';
	}

	if($args{'middle_names'}) {
		if($middle_name) {
			$rc .= " $middle_name";
		} elsif($name =~ /\s([A-Z])\s.+/) {
			# Just an initial has been given
			$rc .= " $1";
		}
	}

	if($surname) {
		if($rc) {
			$rc .= ' ';
		}
		$rc .= normalize_name($surname);
		if(my $suffix = $name_components{'suffix'}) {
			$rc .= " $suffix";
		}
	} elsif($args{'print_unknown'}) {
		$rc .= ' ?';
	}

	if($has_maiden_name && $self->surname()) {
		$rc .= ' (nee ' . normalize_name($self->surname()) . ')';
	}

	if(!defined($rc)) {
		$args{'include_years'} = 1;
		$rc = 'Unknown person ';
	}

	if($args{'include_years'}) {
		my $dob = get_value({ person => $self, value => 'birth date' });

		my $yob;	# Year of birth
		if($dob && ($dob =~ /.*?(\d{3,4})/)) {
			if($dob =~ /^(Abt|ca?)\.?\s*(.+)/i) {
				my $year = $2;
				if($year =~ /^[A-Z]{3}\s(.+)/i) {
					$yob = "c$1";	# Get the "1951" from "Feb 1951"
				}
				$yob = "c$year";
			} else {
				$yob = $1;
			}
		} else {
			$dob = get_value({ person => $self, value => 'baptism date' });
			if($dob && ($dob =~ /.*?(\d{3,4})/)) {
				$yob = "c$1";
			}
		}

		my $dod = get_value({ person => $self, value => 'death date' });

		my $yod;	# Year of death
		if($dod && ($dod =~ /.*?(\d{3,4})/)) {
			$yod = $1;
		} else {
			$dod = get_value({ person => $self, value => 'burial date' });
			if($dod && ($dod =~ /.*?(\d{3,4})/)) {
				$yod = "c$1";
			}
		}

		$rc .= ' ' if($yob || $yod);

		if($yob) {
			$rc .= "$yob ";
		}

		$rc .= '-' if($yob || $yod);

		if($yod) {
			return "$rc $yod";
		}
	}

	if(!defined($rc)) {
		red_warning({ "Can't determine the name for record " . $self->{'xref'} });
		return '';
	}

	$rc =~ tr/"/'/;	# fix issues with Graphviz and others - compatibility with ged2site

	return $rc;
}

sub red_warning
{
	my %params = (ref($_[0]) eq 'HASH') ? %{$_[0]} : @_;

	die "What do you want to say?" unless($params{'warning'});

	if($params{'person'}) {
		my @call_details = caller(0);
		warn colored(['red'], $params{'person'}->as_string(), ': ', $params{'warning'}, ' at line ', $call_details[2]);
	} else {
		warn colored(lcfirst($params{'warning'}), 'red');
	}
	# my $i = 0;
	# while((my @call_details = (caller($i++)))) {
		# print STDERR "\t", colored($call_details[1] . ':' . $call_details[2] . ' calling function ' . $call_details[3], 'red'), "\n";
	# }
}

# Cache gedcom values
sub get_value
{
	my %params = (ref($_[0]) eq 'HASH') ? %{$_[0]} : @_;

	my $person = $params{'person'};
	my $value = $params{'value'};

	my $xref = $person->{'xref'};
	tie (my %values, 'Tie::Static');

	if(defined($values{$xref}{$value})) {
		return $values{$xref}{$value};
	}

	my $rc = $person->get_value($value);
	$values{$xref}{$value} = $rc;
	return $rc;
}

sub normalize_name
{
	# my $name = shift;

	# my $rc;
	# foreach my $word(split(/-| |'/, lc($name))) {
		# $rc .= '-' if($rc && ($name =~ /-/));
		# $rc .= "'" if($rc && ($name =~ /'/));
		# $rc .= ' ' if($rc && ($name =~ / /));
		# $rc .= ucfirst($word);
	# }

	# return $rc;

	return Lingua::EN::NameCase::nc(shift);
}

# Compare two dates. Approximate dates are compared.
# TODO: handle when only months are known
sub datecmp($$)
{
	my ($left, $right) = @_;

	if((!defined($left)) || !defined($right)) {
		# Shouldn't happen with the prototype
		print STDERR "\n";
		if(!defined($left)) {
			print STDERR "BUG: left not defined\n";
		}
		if(!defined($right)) {
			print STDERR "BUG: right not defined\n";
		}
		my $i = 0;
		while((my @call_details = caller($i++))) {
			print STDERR "\t", colored($call_details[2] . ' of ' . $call_details[1], 'red'), "\n";
		}
		return 0;
	}
	if(!ref($left)) {
		if($left =~ /^(Abt|ca?)\s+(.+)/i) {
			$left = $2;
		}
		if($left !~ /^\d{3,4}$/) {
			my @l = $dfg->parse_datetime($left);
			my $rc = $l[1] || $l[0];
			if(!defined($rc)) {
				print STDERR "Date parse failure: ($left):\n";
				my $i = 0;
				while((my @call_details = caller($i++))) {
					print STDERR "\t", colored($call_details[2] . ' of ' . $call_details[1], 'red'), "\n";
				}
				return 0;
			}
			$left = $rc;
		}
	}
	if(!ref($right)) {
		if($right =~ /^bef/i) {
			print STDERR "$right: Before not handled\n";
			my $i = 0;
			while((my @call_details = caller($i++))) {
				print STDERR "\t", colored($call_details[2] . ' of ' . $call_details[1], 'red'), "\n";
			}
			return 0;
		}
		if($right =~ /^(Abt|ca?)\s+(.+)/i) {
			$right = $2;
		}
		if($right =~ /^\d{3,4}$/) {
			if(ref($left)) {
				return $left->year() <=> $right;
			} else {
				return $left <=> $right;
			}
		}
		# if(!$dfg->parse_datetime($right)) {
			# my $i = 0;
			# while((my @call_details = caller($i++))) {
				# print STDERR "\t", colored($call_details[2] . ' of ' . $call_details[1], 'red'), "\n";
			# }
			# die join('<=>', @_);
		# }
		my @r = $dfg->parse_datetime($right);
		die $right if(!defined($r[0]));
		$right = $r[0];
	}
	if((!ref($left)) && ref($right)) {
		return $left <=> $right->year();
	}
	if(ref($left) && (!ref($right))) {
		return $left->year() <=> $right;
	}

	return $left <=> $right;
}
